<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>GLSL で画像にフィルタをかけてみるテスト（改9） - js do it</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<!-- generated by: jsdo.it - http://jsdo.it/cx20/gXCn -->
<!-- Copyright cx20 - http://jsdo.it/cx20 -->
<!-- Licensed under MIT License - http://www.opensource.org/licenses/mit-license.php -->
<script id="fs" type="x-shader/x-fragment">
// forked from pyalot's "Hexagon Sampling" https://www.shadertoy.com/view/ls23Dc
precision mediump float;

uniform sampler2D texture;
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;
varying vec4 vColor;
varying vec2 vTextureCoord;

float PI = 3.14159265359;
float TAU = 2.0*PI;
float deg30 = TAU/12.0;

// http://www.gamedev.net/page/resources/_/technical/game-programming/coordinates-in-hexagon-based-tile-maps-r1800
// nearest hexagon sampling, not quite sure if it's correct

float hexDist(vec2 a, vec2 b){
    vec2 p = abs(b-a);
    float s = sin(deg30);
    float c = cos(deg30);
    
    float diagDist = s*p.x + c*p.y;
    return max(diagDist, p.x)/c;
}

vec2 nearestHex(float s, vec2 st){
    float h = sin(deg30)*s;
    float r = cos(deg30)*s;
    float b = s + 2.0*h;
    float a = 2.0*r;
    float m = h/r;

    vec2 sect = st/vec2(2.0*r, h+s);
    vec2 sectPxl = mod(st, vec2(2.0*r, h+s));
    
    float aSection = mod(floor(sect.y), 2.0);
    
    vec2 coord = floor(sect);
    if(aSection > 0.0){
        if(sectPxl.y < (h-sectPxl.x*m)){
            coord -= 1.0;
        }
        else if(sectPxl.y < (-h + sectPxl.x*m)){
            coord.y -= 1.0;
        }

    }
    else{
        if(sectPxl.x > r){
            if(sectPxl.y < (2.0*h - sectPxl.x * m)){
                coord.y -= 1.0;
            }
        }
        else{
            if(sectPxl.y < (sectPxl.x*m)){
                coord.y -= 1.0;
            }
            else{
                coord.x -= 1.0;
            }
        }
    }
    
    float xoff = mod(coord.y, 2.0)*r;
    return vec2(coord.x*2.0*r-xoff, coord.y*(h+s))+vec2(r*2.0, s);
}

void main(void){
    vec2 uv = gl_FragCoord.xy/resolution.xy;
    float s = resolution.x/80.0;
    vec2 nearest = nearestHex(s, gl_FragCoord.xy);
    vec4 texel = texture2D(texture, -nearest/resolution.xy, -100.0);
    float dist = hexDist(gl_FragCoord.xy, nearest);
    
    float luminance = (texel.r + texel.g + texel.b)/3.0;
    //float interiorSize = luminance*s;
    float interiorSize = s;
    float interior = 1.0 - smoothstep(interiorSize-1.0, interiorSize, dist);
    //gl_FragColor = vec4(dist);
    gl_FragColor = vec4(texel.rgb*interior, 1.0);
    //gl_FragColor = vec4(nearest, 0.0, 1.0);
}
</script>

<script id="vs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec4 color;
attribute vec2 textureCoord;
uniform   mat4 mvpMatrix;
varying   vec4 vColor;
varying   vec2 vTextureCoord;

void main(void){
	vColor        = color;
	vTextureCoord = textureCoord;
	gl_Position   = mvpMatrix * vec4(position, 1.0);
}
</script>

<canvas id="canvas"></canvas>


<script type="text/javascript" src="../../lib/minMatrix.js"></script>

<script type="text/javascript" src="index.js"></script>
</body>
</html>
