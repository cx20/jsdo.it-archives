<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>[WebGL] Grimoire.js でポストエフェクトを試してみるテスト（その２１） - js do it</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<!-- generated by: jsdo.it - http://jsdo.it/cx20/snhP -->
<!-- Copyright cx20 - http://jsdo.it/cx20 -->
<!-- Licensed under MIT License - http://www.opensource.org/licenses/mit-license.php -->
<!-- grimoire-preset-basic.js v1.11.0 -->
<script src="https://unpkg.com/grimoirejs-preset-basic@1.11.0/register/grimoire-preset-basic.js"></script>

<script type="text/sort" id="shader" typeName="postEffect">
@Pass{
// FS_PREC(mediump, float)
FS_PREC(highp, float)  // iPhone で _time / 1000 が桁落ちする問題の対応
varying vec2 vTexCoord;

#ifdef VS
attribute vec3 position;
attribute vec2 texCoord;

void main(){
  gl_Position = vec4(position,1);
  vTexCoord = texCoord;
}

#endif

#ifdef FS
uniform sampler2D texture;
uniform sampler2D noiseTexture;
uniform float _time;
uniform vec2 _viewportSize;

// forked from jt's "RainScreen" https://www.shadertoy.com/view/4ljXDy
    
// RainScreen - written 2015 by Jakob Thomsen
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

// iq's hash function from https://www.shadertoy.com/view/MslGD8
vec2 hash( vec2 p ) { p=vec2(dot(p,vec2(127.1,311.7)),dot(p,vec2(269.5,183.3))); return fract(sin(p)*18.5453); }

float simplegridnoise(vec2 v)
{
    float time = _time / 1000.0;
    float s = 1. / 256.;
    vec2 fl = floor(v), fr = fract(v);
    float mindist = 1e9;
    for(int y = -1; y <= 1; y++)
        for(int x = -1; x <= 1; x++)
        {
            vec2 offset = vec2(x, y);
            vec2 pos = .5 + .5 * cos(2. * PI * (time *.1 + hash(fl+offset)) + vec2(0,1.6));
            mindist = min(mindist, length(pos+offset -fr));
        }
    
    return mindist;
}

float blobnoise(vec2 v, float s)
{
    return pow(.5 + .5 * cos(PI * clamp(simplegridnoise(v)*2., 0., 1.)), s);
}

vec3 blobnoisenrm(vec2 v, float s)
{
    vec2 e = vec2(.01,0);
    return normalize(
           vec3(blobnoise(v + e.xy, s) - blobnoise(v -e.xy, s),
                blobnoise(v + e.yx, s) - blobnoise(v -e.yx, s),
                1.0));
}

float blobnoises(vec2 uv, float s)
{
    float time = _time / 1000.0;
    float h = 0.0;
    const float n = 3.0;
    for(float i = 0.0; i < n; i++)
    {
        vec2 p = vec2(0.0, 1.0 * time * (i + 1.0) / n) + 1.0 * uv;
        h += pow(0.5 + 0.5 * cos(PI * clamp(simplegridnoise(p * (i + 1.0)) * 2.0, 0.0, 1.0)), s);
    }
    
    return h / n;
}

vec3 blobnoisenrms(vec2 uv, float s)
{
    float d = 0.01;
    return normalize(
           vec3(blobnoises(uv + vec2(  d, 0.0), s) - blobnoises(uv + vec2( -d, 0.0), s),
                blobnoises(uv + vec2(0.0,   d), s) - blobnoises(uv + vec2(0.0,  -d), s),
                d));
}

void main()
{
    vec2 r = vec2(1.0, _viewportSize.y / _viewportSize.x);
    vec2 uv = gl_FragCoord.xy / _viewportSize.xy;
    vec3 n = blobnoisenrms(25.0 * uv * r, 1.);
    gl_FragColor = texture2D(texture, uv + 0.05 * n.xy);
}
#endif
}
</script>

<script type="text/goml" id="canvas">
  <goml width="fit" height="fit">
    <renderer>
      <render-buffer name="rb"/>
      <texture-buffer name="bb1"/>
      <render-scene out="bb1" depthBuffer="rb"/>
      <render-quad material="new(postEffect)" texture="backbuffer(bb1)" noiseTexture="../../assets/M/p/s/t/Mpsth.png" out="default"/>
    </renderer>
    <goml.components>
    </goml.components>
    <image-texture src="../../assets/A/k/w/j/AkwjW.jpg"/>
    <scene>
      <camera class="camera" near="0.01" far="40.0" aspect="1.0" fovy="45d" position="0,0,3">
        <camera.components>
          <MouseCameraControl />
        </camera.components>
      </camera>
      <mesh geometry="cube" texture="query(image-texture)" >
        <object.components>
          <!-- <Rotate/> -->
        </object.components>
      </mesh>
    </scene>
  </goml>
</script>



<script type="text/javascript" src="index.js"></script>
</body>
</html>
