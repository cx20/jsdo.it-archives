<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>GLSL で print デバッグしてみるテスト - js do it</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<!-- generated by: jsdo.it - http://jsdo.it/cx20/Amgk -->
<!-- Copyright cx20 - http://jsdo.it/cx20 -->
<!-- Licensed under MIT License - http://www.opensource.org/licenses/mit-license.php -->
<script id="fs" type="x-shader/x-fragment">
// forked from http://glslsandbox.com/e#19171.3
precision mediump float;

uniform sampler2D texture;
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;
varying vec4 vColor;
varying vec2 vTextureCoord;

//3x5 digit sprites stored in "15 bit" numbers
/*
███     111
  █     001
███  -> 111  -> 111001111100111 -> 29671
█       100
███     111
*/
/*
        000
        000
     -> 000  -> 000000000000010 -> 2
        000
 █      010
*/
/*
        000
 █      010
███  -> 111  -> 000010111010000 -> 1488
 █      010
        000
*/
/*
        000
        000
███  -> 111  -> 000000111000000 -> 448
        000
        000
*/

float c_0 = 31599.0;
float c_1 = 9362.0;
float c_2 = 29671.0;
float c_3 = 29391.0;
float c_4 = 23497.0;
float c_5 = 31183.0;
float c_6 = 31215.0;
float c_7 = 29257.0;
float c_8 = 31727.0;
float c_9 = 31695.0;

float c_x = 23213.0;
float c_y = 23186.0;
float c_colon = 1040.0;
float c_period = 2.0;
float c_plus  = 1488.0;
float c_minus = 448.0;

//returns 0/1 based on the state of the given bit in the given number
float getBit(float num,float bit)
{
	num = floor(num);
	bit = floor(bit);
	
	return float(mod(floor(num/pow(2.,bit)),2.) == 1.0);
}

float Sprite3x5(float sprite,vec2 p)
{
	float bounds = float(all(lessThan(p,vec2(3,5))) && all(greaterThanEqual(p,vec2(0,0))));
	
	return getBit(sprite,(2.0 - p.x) + 3.0 * p.y) * bounds;
}

float Digit(float num,vec2 p)
{
	num = mod(floor(num),10.0);
	
	if(num == 0.0) return Sprite3x5(c_0,p);
	if(num == 1.0) return Sprite3x5(c_1,p);
	if(num == 2.0) return Sprite3x5(c_2,p);
	if(num == 3.0) return Sprite3x5(c_3,p);
	if(num == 4.0) return Sprite3x5(c_4,p);
	if(num == 5.0) return Sprite3x5(c_5,p);
	if(num == 6.0) return Sprite3x5(c_6,p);
	if(num == 7.0) return Sprite3x5(c_7,p);
	if(num == 8.0) return Sprite3x5(c_8,p);
	if(num == 9.0) return Sprite3x5(c_9,p);
	
	return 0.0;
}

float debugPrint( vec2 m, vec2 p ) {
	float c = 0.0;

	//Mouse X position
	vec2 cpos = vec2(1,26);	
	c += Sprite3x5(c_x,floor(p-cpos));
	cpos.x += 4.;
	c += Sprite3x5(c_colon,floor(p-cpos));
	cpos.x += 4.;
	
	//c += Digit(m.x/1000.,floor(p-cpos));
	c += m.x > 0.0 ? Sprite3x5(c_plus,floor(p-cpos)) : Sprite3x5(c_minus,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(abs(m.x/100.),floor(p-cpos));
	cpos.x += 4.;
	c += Digit(abs(m.x/10.),floor(p-cpos));
	cpos.x += 4.;
	c += Digit(abs(m.x),floor(p-cpos));
	cpos.x += 4.;
	c += Sprite3x5(c_period,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(fract(m.x)*10.0,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(fract(m.x)*100.0,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(fract(m.x)*1000.0,floor(p-cpos));
	
	//Mouse Y position
	cpos = vec2(1,20);
	c += Sprite3x5(c_y,floor(p-cpos));
	cpos.x += 4.;
	c += Sprite3x5(c_colon,floor(p-cpos));
	cpos.x += 4.;
	
	//c += Digit(m.y/1000.,floor(p-cpos));
	c += m.y > 0.0 ? Sprite3x5(c_plus,floor(p-cpos)) : Sprite3x5(c_minus,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(abs(m.y/100.),floor(p-cpos));
	cpos.x += 4.;
	c += Digit(abs(m.y/10.),floor(p-cpos));
	cpos.x += 4.;
	c += Digit(abs(m.y),floor(p-cpos));
	cpos.x += 4.;
	c += Sprite3x5(c_period,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(fract(m.y)*10.0,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(fract(m.y)*100.0,floor(p-cpos));
	cpos.x += 4.;
	c += Digit(fract(m.y)*1000.0,floor(p-cpos));
	
	return c;
}

void main( void ) {
	vec2 p = ( gl_FragCoord.xy /resolution.xy ) * vec2(64,32);
	vec2 m = floor(mouse * resolution);
	//vec2 m = gl_FragCoord.xy; // vec2(1.0 / 3.0); // resolution/2.0;

	float c = 0.0;
	c = debugPrint( m, p );

	gl_FragColor = vec4( vec3( c ), 1.0 );
}
</script>

<script id="vs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec4 color;
attribute vec2 textureCoord;
uniform   mat4 mvpMatrix;
varying   vec4 vColor;
varying   vec2 vTextureCoord;

void main(void){
	vColor        = color;
	vTextureCoord = textureCoord;
	gl_Position   = mvpMatrix * vec4(position, 1.0);
}
</script>

<canvas id="canvas"></canvas>

<script type="text/javascript" src="../../lib/minMatrix.js"></script>

<script type="text/javascript" src="index.js"></script>
</body>
</html>
