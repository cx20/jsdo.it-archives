<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>forked: Waterfall  (WebGL2 + Oimo.js) - js do it</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<!-- generated by: jsdo.it - http://jsdo.it/cx20/mxCK -->
<!-- Copyright cx20 - http://jsdo.it/cx20 -->
<!-- Licensed under MIT License - http://www.opensource.org/licenses/mit-license.php -->
<script id="vs" type="x-shader/x-vertex">#version 300 es
#define PI 3.14159265359
#define PI2 ( PI * 2.0 )

layout(location=0) in vec3 position;
layout(location=1) in vec3 normal;
layout(location=2) in vec2 textureCoord;
layout(location=3) in vec3 offset;
layout(location=4) in vec4 rot;
uniform mat4 pMatrix;
out vec3 vColor;
out vec2 vTextureCoord;

mat4 lookAt(in vec3 eye, in vec3 center, in vec3 up)
{
    vec3 w = normalize(eye - center);
    vec3 u = normalize(cross(up, w));
    vec3 v = normalize(cross(w, u));
    return mat4(
        u.x, v.x, w.x, 0.0,
        u.y, v.y, w.y, 0.0,
        u.z, v.z, w.z, 0.0,
        -dot(u, eye), -dot(u, eye), -dot(w, eye), 1.0);    
}    

vec3 qtransform(in vec4 q, in vec3 p)
{
	return p + 2.0 * cross(cross(p, q.xyz) - q.w * p, q.xyz);
}
      
vec3 hsv(in float h, in float s, in float v)
{
    return mix(vec3(1.0), clamp((abs(fract(
        h + vec3(3.0, 2.0, 1.0) / 3.0) * 6.0 - 3.0) - 1.0), 0.0, 1.0), s) * v;
}

float hash(in float n)
{
    return fract(sin(n)*753.5453123);
}
    
void main()
{
    vTextureCoord = textureCoord;
        mat4 vMatrix = lookAt(
        vec3(0.0, 0.0, 40.0),
        vec3(0.0), vec3(0.0, 1.0, 0.0));
    float seed =  float(gl_InstanceID);
    vec3 nor = qtransform(rot, normal);
    vec3 col = hsv(0.55 + hash(seed + 123.123) * 0.1, 0.8 + hash(seed + 456.456) * 0.2, 0.8 + hash(seed + 789.789) * 0.2);
    vec3 light = normalize(vec3(1.0));
    vColor = col * max(dot(light, (vMatrix * vec4(nor, 0.0)).xyz), 0.4);
    vec3 pos = qtransform(rot, position);
    pos += offset;
    gl_Position = pMatrix * vMatrix * vec4(pos, 1.0);      
}
</script>

<script id="fs" type="x-shader/x-fragment">#version 300 es
precision mediump float;
uniform sampler2D uTexture;
in  vec2 vTextureCoord;
in  vec3 vColor;
out vec4 fragColor;
void main() 
{
    vec3 col = pow(vColor, vec3(0.6));
    fragColor = texture(uTexture, vTextureCoord) * vec4(col, 1.0);
}
</script>

<!-- Oimo.min.js -->
<script src="../../assets/z/1/s/T/z1sTt.js"></script>
<canvas id="canvas" width="465" height="465"></canvas>
<div id="div0" style="color:white; font-size:12px; position:absolute; top:10px; left:10px;"></div>
<div id="div1" style="color:white; font-size:12px; position:absolute; top:25px; left:10px;"></div>


<script type="text/javascript" src="index.js"></script>
</body>
</html>
