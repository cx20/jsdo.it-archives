<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>GLSL で画像にフィルタをかけてみるテスト（その１４改） - js do it</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<!-- generated by: jsdo.it - http://jsdo.it/cx20/nq5R -->
<!-- Copyright cx20 - http://jsdo.it/cx20 -->
<!-- Licensed under MIT License - http://www.opensource.org/licenses/mit-license.php -->
<script id="fs" type="x-shader/x-fragment">
// forked from iq's "Planet" https://www.shadertoy.com/view/4sf3Rn
precision mediump float;

uniform sampler2D texture;
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;
varying vec4 vColor;
varying vec2 vTextureCoord;

// Created by inigo quilez - iq/2013
// License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

vec3 doit( in vec2 pix) {
    vec2 p = -1.0 + 2.0 * pix;
    p.x *= resolution.x / resolution.y;

    vec3 ro = vec3(0.0, 0.0, 2.5);
    vec3 rd = normalize(vec3(p, -2.0));

    vec3 col = vec3(0.1);

    // intersect sphere
    float b = dot(ro, rd);
    float c = dot(ro, ro) - 1.0;
    float h = b * b - c;
    if (h > 0.0) {
        float t = -b - sqrt(h);
        vec3 pos = ro + t * rd;
        vec3 nor = pos;

        // texture mapping
        vec2 uv;
        uv.x = atan(nor.x, nor.z) / 6.2831 - 0.3 * time; //  - mouse.x/resolution.x;
        uv.y = acos(nor.y) / 3.1416;
        uv.y *= 0.5;
        col = texture2D(texture, uv).xyz;

        float o = smoothstep(0.3, 0.4, col.x);
        col = mix(vec3(0.2, 0.3, 0.4), col, o);

        // lighting
        col *= 0.1 + 0.9 * max(nor.x * 2.0 + nor.z, 0.0);
    } else {
        c = dot(ro, ro) - 10.0;
        h = b * b - c;
        float t = -b - sqrt(h);
        vec3 pos = ro + t * rd;
        vec3 nor = pos;

        vec2 uv;
        uv.x = 16.0 * atan(nor.x, nor.z) / 6.2831 - 0.5 * time; //  - mouse.x/resolution.x;
        uv.y = 2.0 * acos(nor.y) / 3.1416;
        col = texture2D(texture, -uv, 1.0).xyz;
        col *= col;
        col *= 0.7;
        vec3 sta = texture2D(texture, 0.5 * uv, 5.0).xyz;
        sta = sta * sta;
        col += sta * 0.1;

    }

    col = 0.5 * (col + sqrt(col));
    return col;
}

void main(void) {
    // render this with four sampels per pixel
    vec3 col0 = doit((gl_FragCoord.xy + vec2(0.0, 0.0)) / resolution.xy);
    vec3 col1 = doit((gl_FragCoord.xy + vec2(0.5, 0.0)) / resolution.xy);
    vec3 col2 = doit((gl_FragCoord.xy + vec2(0.0, 0.5)) / resolution.xy);
    vec3 col3 = doit((gl_FragCoord.xy + vec2(0.5, 0.5)) / resolution.xy);
    vec3 col = 0.25 * (col0 + col1 + col2 + col3);

    gl_FragColor = vec4(col, 1.0);
}
</script>

<script id="vs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec4 color;
attribute vec2 textureCoord;
uniform   mat4 mvpMatrix;
varying   vec4 vColor;
varying   vec2 vTextureCoord;

void main(void){
	vColor        = color;
	vTextureCoord = textureCoord;
	gl_Position   = mvpMatrix * vec4(position, 1.0);
}
</script>

<canvas id="canvas"></canvas>

<script type="text/javascript" src="../lib/minMatrix.js"></script>

<script type="text/javascript" src="index.js"></script>
</body>
</html>
