<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>GLSL で画像にフィルタをかけてみるテスト（改4） - js do it</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<!-- generated by: jsdo.it - http://jsdo.it/cx20/sL8g -->
<!-- Copyright cx20 - http://jsdo.it/cx20 -->
<!-- Licensed under MIT License - http://www.opensource.org/licenses/mit-license.php -->
<script id="fs" type="x-shader/x-fragment">
// forked from atomek's "Halftone" https://www.shadertoy.com/view/XslGRM
precision mediump float;

uniform sampler2D texture;
uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;
varying vec4 vColor;
varying vec2 vTextureCoord;

float PI = 3.1415926535897932384626433832795;
float PI180 = float(PI / 180.0);

float sind(float a)
{
    return sin(a * PI180);
}

float cosd(float a)
{
    return cos(a * PI180);
}

float added(vec2 sh, float sa, float ca, vec2 c, float d)
{
    return 0.5 + 0.25 * cos((sh.x * sa + sh.y * ca + c.x) * d) + 0.25 * cos((sh.x * ca - sh.y * sa + c.y) * d);
}

void main(void)
{
    // Halftone dot matrix shader
    // @author Tomek Augustyn 2010
    
    // Ported from my old PixelBender experiment
    // https://github.com/og2t/HiSlope/blob/master/src/hislope/pbk/fx/halftone/Halftone.pbk
    
    // Hold and drag horizontally to adjust the threshold

    float threshold = clamp(float(mouse.x / resolution.x) + 0.6, 0.0, 1.0);

    float ratio = resolution.y / resolution.x;
    float coordX = gl_FragCoord.x / resolution.x;
    float coordY = gl_FragCoord.y / resolution.x;
    vec2 dstCoord = vec2(coordX, coordY);
    vec2 srcCoord = vec2(coordX, coordY / ratio);
    vec2 rotationCenter = vec2(0.5, 0.5);
    vec2 shift = dstCoord - rotationCenter;
    
    float dotSize = 3.0;
    float angle = 45.0;
    
    float rasterPattern = added(shift, sind(angle), cosd(angle), rotationCenter, PI / dotSize * 680.0);
    vec4 srcPixel = texture2D(texture, -srcCoord);
        
    float avg = 0.2125 * srcPixel.r + 0.7154 * srcPixel.g + 0.0721 * srcPixel.b;
    float gray = (rasterPattern * threshold + avg - threshold) / (1.0 - threshold);

    // uncomment to see how the raster pattern looks 
    // gray = rasterPattern;
        
    gl_FragColor = vec4(gray, gray, gray, 1.0);
}
</script>

<script id="vs" type="x-shader/x-vertex">
attribute vec3 position;
attribute vec4 color;
attribute vec2 textureCoord;
uniform   mat4 mvpMatrix;
varying   vec4 vColor;
varying   vec2 vTextureCoord;

void main(void){
	vColor        = color;
	vTextureCoord = textureCoord;
	gl_Position   = mvpMatrix * vec4(position, 1.0);
}
</script>

<canvas id="canvas"></canvas>


<script type="text/javascript" src="../../lib/minMatrix.js"></script>

<script type="text/javascript" src="index.js"></script>
</body>
</html>
