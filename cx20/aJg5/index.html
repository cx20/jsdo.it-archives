<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8" />
<title>[WebGL] Grimoire.js でポストエフェクトを試してみるテスト（その２０） - js do it</title>
<meta name="Description" content="" />
<meta name="Keywords"  content="" />

<link rel="stylesheet" type="text/css" media="screen,print" href="style.css" />
</head>
<body>
<!-- generated by: jsdo.it - http://jsdo.it/cx20/aJg5 -->
<!-- Copyright cx20 - http://jsdo.it/cx20 -->
<!-- Licensed under MIT License - http://www.opensource.org/licenses/mit-license.php -->
<!-- grimoire-preset-basic.js v1.11.0 -->
<script src="https://unpkg.com/grimoirejs-preset-basic@1.11.0/register/grimoire-preset-basic.js"></script>

<script type="text/sort" id="shader" typeName="postEffect">
@Pass{
// FS_PREC(mediump, float)
FS_PREC(highp, float)  // iPhone で _time / 1000 が桁落ちする問題の対応
varying vec2 vTexCoord;

#ifdef VS
attribute vec3 position;
attribute vec2 texCoord;

void main(){
  gl_Position = vec4(position,1);
  vTexCoord = texCoord;
}

#endif

#ifdef FS
uniform sampler2D texture;
uniform sampler2D noiseTexture;
uniform float _time;
uniform vec2 _viewportSize;

// forked from dmmn's "SW Glitch" https://www.shadertoy.com/view/MtfSz2
vec3 rgb2hsv(vec3 c){
    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
    vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

vec3 hsv2rgb(vec3 c){
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

float rand(float n){return fract(sin(n) * 43758.5453123);}

float rand(vec2 n) { 
    return fract(sin(dot(n, vec2(12.9898, 4.1414))) * 43758.5453);
}

// MAIN
void main() {
    float time = _time / 1000.0;
    vec2 uv = gl_FragCoord.xy / _viewportSize.xy;
    vec4 color = texture2D(texture, uv);
    
    // blue
    vec3 hsvColor = rgb2hsv(color.rgb);
    hsvColor.x = .59;
    hsvColor.y = clamp(hsvColor.y, .4, .6 );
    
    float noise = rand(1. + gl_FragCoord.y + gl_FragCoord.x * time);
    hsvColor.z += noise * .2;
    
    // vertical lines
    hsvColor.z += .2 * step(mod(gl_FragCoord.x, 3.), 1.);
    
    // horizontal lines
    float offset = rand(gl_FragCoord.y * gl_FragCoord.x + gl_FragCoord.x * time) * 10.;
    float ratio = step(rand(50. + gl_FragCoord.y + gl_FragCoord.x * time), .2);
    float timeShift = cos(time) * 200.;
    hsvColor.z += .4 * step(mod(gl_FragCoord.y + offset + time * 400. + timeShift, 200.), 10.) * ratio;
    
    // random rectangles
    //float offset2 = rand(ceil(gl_FragCoord.y / 100.) * floor(time * 20.));
    //hsvColor.z += .15 * step(mod(gl_FragCoord.y + time + offset2 * 100. + 100., 200.), 100.);
    
    // horizontal rectangles
    float offset3 = rand(ceil(gl_FragCoord.y * 0.01));
    hsvColor.z += .15 * step(mod(gl_FragCoord.y + time * 15000., 2000. + 2000. * offset3), 400. + 800. * offset3);
    
    color.rgb = hsv2rgb(hsvColor);
    
    gl_FragColor = color;
}
#endif
}
</script>

<script type="text/goml" id="canvas">
  <goml width="fit" height="fit">
    <renderer>
      <render-buffer name="rb"/>
      <texture-buffer name="bb1"/>
      <render-scene out="bb1" depthBuffer="rb"/>
      <render-quad material="new(postEffect)" texture="backbuffer(bb1)" noiseTexture="../../assets/M/p/s/t/Mpsth.png" out="default"/>
    </renderer>
    <goml.components>
    </goml.components>
    <image-texture src="../../assets/A/k/w/j/AkwjW.jpg"/>
    <scene>
      <camera class="camera" near="0.01" far="40.0" aspect="1.0" fovy="45d" position="0,0,6">
        <camera.components>
          <MouseCameraControl />
        </camera.components>
      </camera>
      <mesh geometry="cube" texture="query(image-texture)" >
        <object.components>
          <Rotate/>
        </object.components>
      </mesh>
    </scene>
  </goml>
</script>



<script type="text/javascript" src="index.js"></script>
</body>
</html>
